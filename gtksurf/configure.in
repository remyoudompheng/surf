dnl ------------------------
dnl configure.in for gtksurf
dnl ------------------------

AC_PREREQ(2.13)
AC_INIT(src/main.cc)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gtksurf, 0.1)
CF_VERBOSE


dnl some standard checks:
dnl ---------------------

AC_PROG_CC
AC_PROG_CXX
AC_AIX
AC_ISC_POSIX
AC_MINIX
AC_HEADER_STDC


dnl check if C++ compiler really works:
dnl -----------------------------------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS

JOJO_CXX_WARNINGS
JOJO_CXX_NAMESPACES
JOJO_CXX_STDNAMESPACE
JOJO_CXX_STRINGSTREAM

AC_LANG_RESTORE

if test "$GCC" = "no" ; then
	case `uname` in
	IRIX*)
		CXXFLAGS="-LANG:std $CXXFLAGS"
		;;
	esac
fi



dnl include /usr/local/lib and /usr/local/include in search paths:
dnl --------------------------------------------------------------

if test -d "/usr/local/lib/"; then
	LDFLAGS="-L/usr/local/lib/ $LDFLAGS"
fi
if test -d "/usr/local/include/"; then
	CPPFLAGS="-I/usr/local/include/ $CPPFLAGS"
fi

dnl standard headers:
dnl =================

AC_CHECK_HEADERS(unistd.h)


dnl do we have getopt() and (optionally) GNU getopt_long()?
dnl =======================================================

AC_CHECK_FUNC(getopt,, AC_MSG_ERROR([Sorry: can't find getopt() function]))
AC_CHECK_HEADER(getopt.h)
AC_CHECK_LIB(gnugetopt, main)
AC_CHECK_FUNCS(getopt_long)


dnl check if we have GTK >= 1.2.0
dnl =============================

AM_PATH_GTK(1.2.0)

CPPFLAGS="$CPPFLAGS $GTK_CFLAGS"
LDFLAGS="$LDFLAGS $GTK_LDFLAGS"
LIBS="$LIBS $GTK_LIBS"

dnl checks for libgdk-pixbuf:
dnl =========================

AM_PATH_GDK_PIXBUF
if test "x$no_gdk_pixbuf" != "xyes"; then
  CPPFLAGS="$CPPFLAGS $GDK_PIXBUF_CFLAGS"
  LIBS="$LIBS $GDK_PIXBUF_LIBS"
  AC_DEFINE(HAVE_LIBGDK_PIXBUF)
fi

dnl check if we have a gdk_draw_bitmap() function:
dnl ==============================================

AC_CHECK_FUNCS(gdk_draw_bitmap)

dnl check for libglade:
dnl ===================

AM_PATH_LIBGLADE
CPPFLAGS="$CPPFLAGS $LIBGLADE_CFLAGS"
LIBS="$LIBS $LIBGLADE_LIBS"

dnl check for gtkgl
dnl ===============

AM_PATH_GTKGL
if test "x$have_gtkgl" = "xyes"; then
  LIBS="$LIBS $GTKGL_LIBS"
  CPPFLAGS="$CPPFLAGS $GTKGL_CFLAGS"
else
  AC_MSG_ERROR([You don't have GtkGLArea!])
fi


AM_MISSING_PROG(HELP2MAN, help2man, $missing_dir)
AM_MISSING_PROG(SED, sed, $missing_dir)

AC_SUBST(CPPFLAGS)

AC_OUTPUT(Makefile
          src/Makefile
          pkgdata/Makefile
          docs/Makefile)
