dnl ---------------------
dnl configure.in for surf
dnl ---------------------

AC_PREREQ(2.13)
AC_INIT(src/main.cc)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(surf, 1.1.0)
CF_VERBOSE

dnl check options:
dnl --------------

AC_ARG_WITH(gmp-prefix,[  --with-gmp-prefix=PFX   Prefix where GMP is installed (optional)],
            gmp_prefix="$withval", gmp_prefix="")


dnl some standard checks:
dnl ---------------------

AC_PROG_CC
AC_PROG_CXX
AC_AIX
AC_ISC_POSIX
AC_MINIX
AC_HEADER_STDC
AM_PROG_LEX
AC_PROG_YACC
AC_PROG_RANLIB


dnl check if C++ compiler really works:
dnl -----------------------------------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS

AC_CXX_FUNCTION_NONTYPE_PARAMETERS
if test "$ac_cv_cxx_function_nontype_parameters" = "no"; then
	AC_MSG_ERROR(["Sorry, your compiler is not smart enough to build surf"])
fi

ICE_CXX_EXPLICIT_TEMPLATE_INSTANTIATION

JOJO_CXX_WARNINGS
JOJO_CXX_NAMESPACES
JOJO_CXX_STDNAMESPACE

AC_LANG_RESTORE

if test "$GCC" = "no" ; then
	case `uname` in
	IRIX*)
		CXXFLAGS="-LANG:std $CXXFLAGS"
		;;
	esac
fi




dnl do we have getopt() and (optionally) GNU getopt_long()?

AC_CHECK_FUNC(getopt,, AC_MSG_ERROR([Sorry: can't find getopt() function]))
AC_CHECK_HEADER(getopt.h, AC_CHECK_FUNCS(getopt_long))

dnl check for gethostname prototype in unistd.h:
dnl --------------------------------------------

AC_CHECK_HEADERS(unistd.h)

AC_CACHE_CHECK(whether there's a gethostname prototype in unistd.h,
ac_cv_gethostname_has_prototype,
AC_EGREP_HEADER(gethostname, unistd.h,
	        ac_cv_gethostname_has_prototype=yes,
 	        ac_cv_gethostname_has_prototype=no
	        AC_DEFINE(NO_GETHOSTNAME_PROTO)))



dnl check for libraries:
dnl --------------------

dnl math lib and (g)zlib:

RS_CHECK_LIB(m, main)
AC_CHECK_LIB(z,  main,,
	     AC_CHECK_LIB(gz, main,,
                          AC_MSG_ERROR([Sorry: Can't find libz/libgz.])))

dnl jpeg library and jpeglib.h (optional):

AC_CHECK_HEADER(jpeglib.h,
	        AC_CHECK_LIB(jpeg, main))

dnl check for tiff library and header file (optional):

tifflibfound="no"
AC_CHECK_HEADER(tiffio.h,
	        AC_CHECK_LIB(tiff, main,
	                     tifflibfound="yes"
			     AC_DEFINE(HAVE_LIBTIFF)
			     LIBS="$LIBS -ltiff"))

if test "$libtifffound" = "no"
then
dnl (FreeBSD 3.0 has tiff34.so instead of tiff.so and the header files
dnl are in /usr/local/lib/tiff34)
  AC_CHECK_HEADER(tiff34/tiffio.h,
                  AC_CHECK_LIB(tiff34, main,
	                       AC_DEFINE(TIFF_HEADER_34)
		               AC_DEFINE(HAVE_LIBTIFF)
			       LIBS="$LIBS -ltiff34"))
fi
	
dnl gmp library and header file:

if test x"$gmp_prefix" != x ; then
	LDFLAGS="-L$gmp_prefix/lib $LDFLAGS"
	CPPFLAGS="-I$gmp_prefix/include $CPPFLAGS"
fi

AC_CHECK_HEADER(gmp.h,,
		AC_CHECK_HEADER(gmp2/gmp.h,
				AC_DEFINE(GMP2_PREFIX),
				AC_MSG_ERROR([Sorry: can't find header file gmp.h])))

AC_CHECK_LIB(gmp, main,,
             AC_CHECK_LIB(gmp2, main,,
			  AC_MSG_ERROR([Sorry: can't find gmp])))

LIBS="$LIBS $LEXLIB"

AC_OUTPUT(Makefile
          src/Makefile
	  image-formats/Makefile
	  drawfunc/Makefile
	  yaccsrc/Makefile
	  curve/Makefile
	  draw/Makefile
	  misc/Makefile
	  debug/Makefile
	  dither/Makefile)

