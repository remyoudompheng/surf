dnl ---------------------
dnl configure.in for surf
dnl ---------------------

AC_PREREQ(2.13)
AC_INIT(src/main.cc)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(surf, 1.1.0)
CF_VERBOSE


dnl some standard checks:
dnl ---------------------

AC_PROG_CC
AC_PROG_CXX
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_PROG_LEX
AC_PROG_YACC
AC_PROG_RANLIB


dnl check if C++ compiler really works:
dnl -----------------------------------

AC_CXX_FUNCTION_NONTYPE_PARAMETERS
ICE_CXX_EXPLICIT_TEMPLATE_INSTANTIATION
LF_CHECK_CXX_FLAG(-Wall)
JOJO_CXX_NAMESPACES
JOJO_CXX_STDNAMESPACE

if test "$GCC" = "no" ; then
  case `uname` in
  IRIX*)
    CXXFLAGS="-LANG:std $CXXFLAGS"
    ;;
  esac
fi

dnl include /usr/local/lib and /usr/local/include in search paths:
dnl --------------------------------------------------------------

if test -d "/usr/local/lib/"; then
  LDFLAGS="-L/usr/local/lib/ $LDFLAGS"
fi
if test -d "/usr/local/include/"; then
  CPPFLAGS="-I/usr/local/include/ $CPPFLAGS"
fi

dnl checks for header files:
dnl ------------------------

JOJO_STLPORT
AC_CHECK_HEADERS(unistd.h)

dnl FeeBSD doesn't define htonl() in netinet/in.h but in sys/param.h
dnl ----------------------------------------------------------------

AC_MSG_CHECKING([whether htonl() is defined in netinet/in.h])
AC_TRY_COMPILE([
    #include <netinet/in.h>
  ], [
    int n = htonl(0x12345678);
  ],
  AC_DEFINE(HTONL_IN_NETINET_IN_H)
    AC_MSG_RESULT(yes),
  AC_MSG_RESULT(no))

dnl check options:
dnl --------------

dnl do we have getopt() and (optionally) GNU getopt_long()?

AC_CHECK_FUNC(getopt,, AC_MSG_ERROR([Sorry: can't find getopt() function]))
AC_CHECK_HEADER(getopt.h, AC_CHECK_FUNCS(getopt_long))

dnl check for gethostname prototype in unistd.h:

AC_CACHE_CHECK(whether there's a gethostname prototype in unistd.h,
               ac_cv_gethostname_has_prototype,
               AC_EGREP_HEADER(gethostname, unistd.h,
	                       ac_cv_gethostname_has_prototype=yes,
 	                       ac_cv_gethostname_has_prototype=no
                               AC_DEFINE(NO_GETHOSTNAME_PROTO)))


dnl math lib and (g)zlib:
dnl ---------------------

RS_CHECK_LIB(m, main)
AC_CHECK_LIB(z,  main,,
  AC_CHECK_LIB(gz, main,,
    AC_MSG_ERROR([Sorry: Can't find libz/libgz.])))


dnl checks for image libraries (optional):
dnl --------------------------------------

dnl JPEG library and jpeglib.h:

AC_CHECK_HEADER(jpeglib.h,
  AC_CHECK_LIB(jpeg, main))

dnl PNG library and png.h

AC_CHECK_HEADER(png.h,
  AC_CHECK_LIB(png, png_set_rows))

dnl tiff library and header file:

tifflibfound="no"
AC_CHECK_HEADER(tiffio.h,
  AC_CHECK_LIB(tiff, main,
    tifflibfound="yes"
     AC_DEFINE(HAVE_LIBTIFF)
     LIBS="$LIBS -ltiff"))

if test "$libtifffound" = "no"
then
dnl (FreeBSD 3.0 has tiff34.so instead of tiff.so and the header files
dnl are in /usr/local/lib/tiff34)
  AC_CHECK_HEADER(tiff34/tiffio.h,
    AC_CHECK_LIB(tiff34, main,
      AC_DEFINE(TIFF_HEADER_34)
       AC_DEFINE(HAVE_LIBTIFF)
       LIBS="$LIBS -ltiff34"))
fi

dnl check for gmp:
dnl --------------

JOJO_GMP

dnl check for GTS:
dnl --------------
JOJO_GTS

if test "x$have_gts" == "xyes"; then

  dnl OpenGL & GLU:
  dnl -------------
  JOJO_OPENGL

  if test "x$have_opengl" == "xyes"; then

    dnl OpenInventor:
    dnl -------------
    JOJO_INVENTOR

  fi

fi

dnl link with lex-libaray:
dnl ----------------------

LIBS="$LIBS $LEXLIB"

AC_SUBST(CPPFLAGS)

CXXFLAGS_WITHOUT_WALL=`echo $CXXFLAGS | sed s/-Wall//`
AC_SUBST(CXXFLAGS_WITHOUT_WALL)

AM_MISSING_PROG(HELP2MAN, help2man, $missing_dir)
AM_MISSING_PROG(SED, sed, $missing_dir)

AC_OUTPUT(Makefile
          docs/Makefile
          src/Makefile
	  image-formats/Makefile
	  drawfunc/Makefile
	  yaccsrc/Makefile
	  curve/Makefile
	  draw/Makefile
	  misc/Makefile
	  debug/Makefile
	  dither/Makefile
          triangulator/Makefile)

